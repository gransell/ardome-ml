# (C) Ardendo SE 2008
#
# Released under the LGPL

import sys
import os

Import(['local_env'])

local_env.packages( 'boost_thread', 'boost_filesystem', 'boost_system', 'boost_date_time', 'boost_regex', 'boost_python' )

prefix = sys.exec_prefix
if local_env[ 'target' ] == 'vs2003' or local_env[ 'target' ] == 'vs2008' or local_env[ 'target' ] == 'vs2008express' :
	local_env.Append( CPPPATH = [ '%s/include' % prefix ] )
	local_env.Append( LIBPATH = [ '%s/libs' % prefix ] )
elif local_env['target'] == 'osx':
	pass
else:
	version = sys.version_info
	local_env.Append( CPPPATH = [ '%s/include/python%d.%d' % ( prefix, version[ 0 ], version[ 1 ] ) ] )
	local_env.Append( LIBPATH = [ '%s/lib' % prefix ] )
	local_env.Append( LIBS = [ 'python%d.%d' % ( version[ 0 ], version[ 1 ] ) ] )

local_env[ 'SHLIBPREFIX' ] = ''

if local_env[ 'target' ] == 'osx':
	local_env[ 'SHLIBSUFFIX' ] = '.so'
elif local_env[ 'PLATFORM' ] == 'win32':
	local_env[ 'SHLIBSUFFIX' ] = '.pyd'

local_env[ 'win_target_path' ] = '$python_packages'

object = local_env.shared_library( 'openimagelib' + [ '', '_d' ][ int( local_env.debug ) ], [ 'il.cpp', 'py.cpp' ] )

local_env.Install( os.path.join( local_env[ 'python_packages' ], 'aml' ), object )

Return( 'object' )
